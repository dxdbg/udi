#
# SConscript for test binaries
#
import udibuild

Import('topenv')

bin_env = topenv.Clone()

bin_env.Append(CPPPATH = ['#/tests/bin'])

libs = []

sources = bin_env.Glob('*.c', source=True, strings=True)

bin_lib = 'bin_lib.c'

bin_lib_obj = bin_env.Object(bin_lib)

bin_lib_os = 'bin_lib_win.c'

if udibuild.IsLinux():
    bin_lib_os = 'bin_lib_linux.c'

bin_lib_os_obj = bin_env.Object(bin_lib_os)

waitthread = 'waitthread-win.c'

if udibuild.IsUnix():
    waitthread = 'waitthread-posix.c'
    libs.append('pthread')

waitthread_obj = bin_env.Object(waitthread)

bin_env.Program('waitthread', [ waitthread_obj, bin_lib_obj, bin_lib_os_obj ], LIBS=libs)

for source in sources:
    if source == bin_lib or source == bin_lib_os or source == waitthread:
        continue

    bin_env.Program(udibuild.GetFilename(source), [ source, bin_lib_obj, bin_lib_os_obj ])

# vim: ft=python
