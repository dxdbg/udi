#
# SConscript file for udirt_tests
#

import udibuild

Import('topenv')

bin_env = topenv.Clone()

bin_env.Append(CPPPATH = ['#/libudirt', '#/libudicommon', '#/tests/libuditest'])

sources = bin_env.Glob('*.cxx', strings=True, source=True)

libs = ['udirt', 'uditest']

libpath = ['#/build/libudirt', '#/build/tests/libuditest']

bins = dict()

lib_file = 'test_lib.cxx'

lib_file_obj = bin_env.Object(lib_file)

for source in sources:
    if source == lib_file:
        continue

    bins[udibuild.GetFilename(source)] = bin_env.Object(source)

for binary,source in bins.items():
    bin_env.Program(binary, [ source, lib_file_obj ], LIBS=libs, LIBPATH=libpath)

bin_env.Program('all_udirt_tests', [ bins.values(), lib_file_obj ], LIBS=libs, LIBPATH=libpath)
